
plugins {
    id 'application'
}

sourceCompatibility = 21
targetCompatibility = 21

// tag::repositories[]
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
    }
}
// end::repositories[]

// tag::dependencies[]
dependencies {
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.17'
    testImplementation 'com.github.hmcts:fortify-client:1.4.10:all'
}
// end::dependencies[]

task stageFortifyConfig {
    doLast {
        def configFile = file('config/fortify-client.properties')
        if (configFile.exists()) {
            def workspaceRoot = projectDir.parentFile.parentFile
            copy {
                from configFile
                into new File(workspaceRoot, 'config')
            }
        }
    }
}

task copyFortifyReports {
    doLast {
        def reportDir = file('Fortify Scan')
        if (reportDir.exists()) {
            def workspaceRoot = projectDir.parentFile.parentFile
            copy {
                from reportDir
                into new File(workspaceRoot, 'Fortify Scan')
            }
        }
    }
}

task fortifyScan(type: JavaExec)  {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
    dependsOn stageFortifyConfig
    finalizedBy copyFortifyReports
}
